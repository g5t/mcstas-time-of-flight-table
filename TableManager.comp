/*******************************************************************************
* McStas component: TableManager
*
* %I
* Written by: Contributors to the mcstas-time-of-flight-table project
* Date: 2026
* Origin: ESS
*
* Manages a time-of-flight lookup table built from neutron ray time recordings.
*
* %D
* This component builds and outputs a time-of-flight lookup table by collecting
* the per-neutron time values recorded at each TableRecorder position along the
* beamline.
*
* It inserts a pointer (tof_t) and size integer (tof_n) into every neutron ray's
* state via the McCode USERVARS mechanism.  The component-level USERVARS receive
* an instance-index suffix in the generated C code (e.g. tof_t_5, tof_n_5 when
* this component is the 5th in the instrument); TableManager therefore computes
* the byte offsets to these fields at INITIALIZE time and stores them in
* shared globals so that TableSetup and TableRecorder can access the same
* per-particle storage without knowing the suffix at authoring time.
*
* Placement:
*   TableManager should be placed AFTER the last TableRecorder in the instrument.
*   TableSetup should be placed BEFORE all TableRecorders.
*
* Output file format:
*   Space-separated columns, one row per detected neutron.
*   Columns:  t[0]  t[1]  ...  t[n_recorders-1]  weight
*   A header line beginning with '#' describes the columns.
*
* %P
* filename: string, Name of the output file. Default: "tof_table.dat"
* write_file: int, Whether to write the output file at the end of the simulation. Default: 1 (true)
* t_min: double, Minimum time value for binning. Default: 0
* t_max: double, Maximum time value for binning. Default: 0
* t_bins: int, Number of time bins for the output table. Default: 0 (no binning)
*
* %E
*******************************************************************************/

DEFINE COMPONENT TableManager

SETTING PARAMETERS (
  string filename=0, 
  int write_file=1,
  t_min=0, 
  t_max=0, 
  int t_bins=0
)

SHARE
%{
#ifdef TOF_TABLE_MANAGER
#error "Multiple TableManager components in the instrument are not supported."
#endif
#define TOF_TABLE_MANAGER
%include "tof-table-lib"
%}

USERVARS
%{
  /* Per-particle dynamic arrays of recorded times, probabilities, distances, and their allocated size.
   * These fields are inserted into every neutron ray's state struct.
   * NOTE: the generated C code appends an instance-index suffix, e.g. tof_t_5. */
  double * table_manager_t;
  double * table_manager_p;
  int table_manager_n;
%}

DECLARE
%{
  struct TableManagerData * table;
  char * real_filename;
%}

INITIALIZE
%{
  if (t_bins < 0) {
    fprintf(stderr, "TableManager ERROR: t_bins must be non-negative.\n");
    exit(1);
  }
  if (t_bins > 0 && t_max <= t_min) {
    fprintf(stderr, "TableManager ERROR: t_max must be greater than t_min when t_bins is positive.\n");
    exit(1);
  }
  // Insert the actual names of the USERVARS, these must match those in the USERVARS section above:
  table_manager_state_finalize(INDEX_CURRENT_COMP, "table_manager_t", "table_manager_p", "table_manager_n");


  if (filename && strcmp(filename, "")) {
    real_filename = filename;
  } else {
    real_filename = (char *)calloc(strlen(NAME_CURRENT_COMP) + 20, sizeof(char));
    sprintf(real_filename, "%s_tof_table.dat", NAME_CURRENT_COMP);
  }

  if (!_tof_table_manager_state){
    fprintf(stderr, "TableManager ERROR: state must be pre-allocated by TableSetup.\n");
    exit(1);
  }
  table = table_manager_data_alloc(_tof_table_manager_state->n_recorders, t_bins ? t_bins : 1, t_min, t_max);
  if (!table) {
    fprintf(stderr, "TableManager ERROR: Failed to allocate component data.\n");
    exit(1);
  }

%}

TRACE
%{
  table_manager_particle_to_table(_particle, table);
  table_manager_particle_free(_particle);
%}

SAVE
%{
  if (write_file){
    table_manager_write_output_file(real_filename, table);
  }
%}

FINALLY
%{
  table_manager_data_free(table);
  table_manager_state_free();
  if (real_filename && real_filename != filename) {
    free(real_filename);
    real_filename = NULL;
  }
%}

END

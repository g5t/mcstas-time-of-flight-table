/*******************************************************************************
* McStas component: TableSetup
*
* %I
* Written by: Contributors to the mcstas-time-of-flight-table project
* Date: 2026
* Origin: ESS
*
* Initialises per-particle storage for the time-of-flight lookup table.
*
* %D
* Place this component BEFORE all TableRecorder components in the beamline.
* At INITIALIZE time it reads the final count of TableRecorder instances
* (which is available because all component INITIALIZE sections complete
* before the first TRACE call).  On each TRACE it allocates the per-particle
* array referenced by the USERVARS that TableManager inserts
* into the neutron ray struct.
*
* Placement:
*   TableSetup must appear BEFORE the first TableRecorder in the instrument.
*   TableManager must appear AFTER the last TableRecorder.
*
* %P
* is_t_zero: int, If 1, the time-of-flight recorded in the table will be the time since it passed through this TableSetup, rather than the time since the neutron was created (t=0). Default: 0
* offset_t_zero: double, If set, the time-of-flight recorded in the table will be offset by this fixed value. Default: UNSET (no offset)
*
* %E
*******************************************************************************/

DEFINE COMPONENT TableSetup

SETTING PARAMETERS (
  int is_t_zero=0,
  double offset_t_zero=UNSET
)

SHARE
%{
%include "tof-table-lib"
%}

DECLARE
%{
%}

INITIALIZE
%{
  table_manager_state_alloc();
  offset_t_zero = is_set(offset_t_zero) ? offset_t_zero : 0;
%}

TRACE
%{
  // The zero-point for the time-of-flight recorded in the table:
  double initial_time = (is_t_zero ? t : 0. ) + offset_t_zero;
  // Setup the in-particle arrays for the time-of-flight and probability recorded by each TableRecorder, and the count of recorders.
  // Each table-recorder adds to tof_t, so we need the negative of t0 to end-up-with time-of-flight since t0.
  table_manager_particle_alloc(_particle, initial_time == 0 ? 0 : -initial_time);
%}

END
